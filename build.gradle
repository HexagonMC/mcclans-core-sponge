plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'java'

sourceCompatibility = 1.8

group = 'nl.riebie'
version = '1.5'
description = 'MCClans allows players to group together by forming clans'

jar.enabled = false

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'minecrell'
        url = 'http://repo.minecrell.net/releases'
    }
    maven {
        name = 'nucleus'
        url = 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.spongepowered:spongeapi:5.0.0'
    compile 'net.minecrell.mcstats:statslite-sponge:0.2.2'
    compile 'io.github.nucleuspowered:nucleus-api:0.22.0-S5.1'
    compile project(':mcclans-api')
}

shadowJar {
    dependencies {
        include dependency(':mcclans-api')
        include dependency('net.minecrell.mcstats:statslite-sponge')
        include dependency('io.github.nucleuspowered:nucleus-api')
    }

    relocate 'net.minecrell.mcstats', 'nl.riebie.mcclans.mcstats'

    Boolean devBuild = System.getenv("DEV_BUILD") as Boolean ?: false;
    Boolean relBuild = System.getenv("REL_BUILD") as Boolean ?: false;
    if (devBuild) {
        Integer buildNumber = System.getenv("BUILD_NUMBER") as Integer ?: 0;
        archiveName = rootProject.name + '-' + project.version + '-DEV-' + buildNumber + '.jar'
    } else if (relBuild) {
        archiveName = rootProject.name + '-' + project.version + '.jar'
    } else {
        archiveName = rootProject.name + '-' + project.version + '-LOCAL' + '.jar'
    }
}

artifacts {
    archives shadowJar
}
