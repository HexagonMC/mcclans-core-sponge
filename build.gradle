plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'java'

sourceCompatibility = 1.8

group = 'nl.riebie'
version = '1.6'
description = 'MCClans allows players to group together by forming clans'

jar.enabled = false

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'bstats'
        url = 'http://repo.bstats.org/content/repositories/releases/'
     }
    maven {
        name = 'nucleus'
        url = 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'
    compile "org.bstats:bstats-sponge:1.2"
    compile 'io.github.nucleuspowered:nucleus-api:1.3.7-S7.0'
    compile files('libs/MCClans-API-1.4-SNAPSHOT.jar')
}

shadowJar {
    dependencies {
        include dependency('nl.riebie:mcclans-api')
        include dependency('io.github.nucleuspowered:nucleus-api')
        include dependency("org.bstats:bstats-sponge:1.2")
    }

    relocate "org.bstats", "nl.riebie.mcclans.shade.org.bstats"

    Boolean devBuild = System.getenv("DEV_BUILD") as Boolean ?: false;
    Boolean relBuild = System.getenv("REL_BUILD") as Boolean ?: false;
    if (devBuild) {
        Integer buildNumber = System.getenv("BUILD_NUMBER") as Integer ?: 0;
        archiveName = rootProject.name + '-' + project.version + '-DEV-' + buildNumber + '.jar'
    } else if (relBuild) {
        archiveName = rootProject.name + '-' + project.version + '.jar'
    } else {
        archiveName = rootProject.name + '-' + project.version + '-LOCAL' + '.jar'
    }
}

artifacts {
    archives shadowJar
}

compileJava.options.encoding = 'UTF-8'
